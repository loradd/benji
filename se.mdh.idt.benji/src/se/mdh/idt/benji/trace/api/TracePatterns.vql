package se.mdh.idt.benji.trace.api

// metamodels
import 'http://www.eclipse.org/emf/2002/Ecore'
import 'http://www.mdh.se/idt/benji/trace/Trace'

// trace link - initial element 
private pattern initial (traceLink : TraceLink, source : EObject) = {
	TraceLink.source(traceLink, source);
}

// trace link - current element
private pattern current (traceLink : TraceLink, target : EObject) = {
	TraceLink.target(traceLink, target); 	
}

// trace link - existing element
pattern existing (target : EObject) {
	find preserved (_source, target); 
} or {
	find created (target); 
}

// trace link - created element
pattern created (target : EObject) = {
	neg find initial (traceLink, _source);  
	find current (traceLink, target);
}

// trace link - deleted element
pattern deleted (source : EObject) = {
	find initial (traceLink, source); 
	neg find current (traceLink, _target); 
}

// trace link - preserved element
pattern preserved (source : EObject, target : EObject) = {
	find initial (traceLink, source); 
	find current (traceLink, target); 
}